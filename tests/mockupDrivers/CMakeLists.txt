add_library(SDL_OR_SDL2 INTERFACE)

find_package(SDL2)
if(SDL2_FOUND)
  target_link_libraries(SDL_OR_SDL2 INTERFACE SDL2::SDL2)
else()
find_package(SDL 1.2)
  if(SDL_FOUND)
    target_link_libraries(SDL_OR_SDL2 INTERFACE ${SDL_LIBRARY})
    target_include_directories(SDL_OR_SDL2 SYSTEM INTERFACE ${SDL_INCLUDE_DIR})
  else()
    message(FATAL_ERROR "Did not find SDL 2 or SDL 1.2 which is required for tests")
  endif()
endif()

add_library(videoMockup STATIC MockupVideoDriver.h MockupVideoDriver.cpp)
target_link_libraries(videoMockup PUBLIC videodrv s25util PRIVATE SDL_OR_SDL2 nowide::static)
target_include_directories(videoMockup INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/..)

add_library(audioMockup STATIC MockupAudioDriver.h MockupAudioDriver.cpp)
target_link_libraries(audioMockup PUBLIC audiodrv)
target_include_directories(audioMockup INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/..)

if(WIN32)
  include(GatherDll)
  if(SDL2_FOUND)
    gather_dll_by_name(SDL2 SDL2.dll)
  else()
    gather_dll_by_name(SDL SDL.dll)
  endif()
endif()
